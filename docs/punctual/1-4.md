![DECODED Banner](images/banner_punctual.png)

# Punctual Visual Workshop 1-4 - Jigging With the Numbers (~1h)

**Goals:**
 - learn how to use *variables*, and why they're useful
 - apply transform functions to modify your visuals
 - use textures (images) in your creations

**Reference:** 
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Setup Cells!

```
!presetview twocolumns
```

 - Left Cell: `Punctual`
 - Right Cell:  Your choice!

---

## Connecting Geometry Altering Functions with `$`

Punctual includes 4 "fixed" transform functions for shifting an image around on the canvas: 
 - `spin [amount] [...]`
 - `zoom [x,y] [...]`
 - `tile [x,y] [...]`
 - `move [x,y] [...]`

These functions can be used in combination with the haskell `$` operator

The least confusing technical description of `$` in haskell I've found is:

> "You usually see $ where standard function application wouldn’t have the necessary associativity and precedence for the context. And what *that* means is you usually see it used instead of parentheses to associate things that otherwise wouldn’t.
> [typeclasses.com](https://typeclasses.com/featured/dollar)

Let's do some testing by applying `spin` to a `hline`- starting with a static `hline`:

```
hline 0 0.2 >> video;
```

It is possible to apply the `spin` transform function using parentheses:

```
spin 1.25 (hline 0 0.2) >> video;
```

However, parentheses can be difficult to match, especially in a complicated punctual statement so let's use `$` for this example instead

```
spin 1.25 $ hline 0 0.2 >> video;
```

Much simpler! `spin` is applied to everything after the `$`! So easy, but...

What about if we want to apply multiple transform functions? Let's look at `spin` in combination with `tile`

First, let's get spin using a changing value - `tri 0.125`:

```
spin (tri 0.125) $ hline 0 0.2 >> video;
```

Next, let's add `tile` as a prefix to the entire statement:

```
tile 4 $ spin (tri 0.125) $ hline 0 0.2 >> video;
```

What's happening here? Is `spin` being applied before `tile` or afterwards?

If we consider what the output looks like, there are 16 (4x4) spinning lines, so a spinning line was created, and then tiled.

We can verify with parentheses:

```
tile 4 (spin (tri 0.125) $ hline 0 0.2) >> video;
```

Everything inside the parentheses is evaluated first, then `tile` is applied

Let's swap `tile` and `spin`:

```
spin (tri 0.125) $ tile 4 $ hline 0 0.2 >> video;
```

Describe what has happened here? If necessary, break the function into pieces, or use parentheses to verify 

---

## New Flexible Geometry Altering Functions

As of Punctual 4.0, a series of more `setf*` functions were provided, that now let us play all sorts of transformation games with any expression!

 - `setfx [x1,x2,...]` - remap `fx` by setting it to the value of a specific expression
 - `setfy [y1,y2,...]` - remap `fy` by setting it to the value of a specific expression
 - `setfxy [x1,y1,x2,y2,...]` - remap `fxy` by setting it to the value of a specific expression

Here's a quirky example using `setfxy`:

```
setfxy (fxy +: (sin fxy * 0.05)) $ circle 0 0.5 >> video
```

---

## Using Variables to Simplify Your Code

By now you will notice that your code statements are getting longer. You may be using multiple transform functions, you may even want to use some of the comparison functions with multiple complex statements ... fitting these statements onto one line can get unwieldy fast!

If we take the `circle`s example from the previous workshop exercises

```
(circle [0,0] 0.25) + (circle [0.15,0.15] 0.35) >> video;
```

We can define the two `circle` statements separately, and store their output in a variable name (`a` and `b` in the example below). Then those variables can be referenced in a third statement. Here is the equivalent statement, using variables:

```
a << (circle [0,0] 0.25);
b << (circle [0.15,0.15] 0.35);
a + b >> video;
```
Nice and simple! Plus we can reuse the variables as many times as we like - let's reuse it with `min` and add some oscillators and functions for fun

```
a << (circle [0,0] 0.25);
b << (circle [0.15,0.15] 0.35);
a + b >> video;
tile 4 $ min (sin a) (tri b) >> video;
```

I think you will agree, this is much more readable - not to mention, you only need to modify the variable in one place, and it will update on both statements that use it

---

## Image (texture) manipulation!

**XXX: todo `img` now `tex*` is deprecated**

`tex` and `texhsv` are functions for accessing textures built from an image file (specified as a web url)

There is one major caveat in using these functions, the images must be hosted on a [CORS compliant](https://developer.mozilla.org/en-US/docs/Web/HTTP/CORS) web server - 

Fortunately, one of the biggest (and most interesting) repositories of permissively licensed images is CORS compliant:

 > [commons.wikimedia.org](https://commons.wikimedia.org/wiki/Category:Images)

Search for an image file, or pick one at random - then find the "Original File" link under the image, then right-click and "Copy link address"

Paste your copied image address in the following examples (or use my example):

```
tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

`texhsv` uses exactly the same syntax as `tex`, but outputs the texture content as a 3-channel `hsv` signal

```
texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

What happens if you use the `hsv` output with `texhsv`?

```
texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> hsv;
```

What about using the `hsv` output with `tex`?

```
tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> hsv;
```

`tex` functions work well in conjuction with the transform functions covered earlier

```
zoom (prox (0.5 + sin 0.03)) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy >> video;
```

---

## Transitions!

To reduce the harshness of what can sometimes be quite jarring changes, we can use a transition operator `<>`.

`<>` takes one argument, a floating point number in seconds to describe how long it should take to replace the original pattern with the new pattern, using a "fade" effect

Continuing on from the example above, what if we changed the image used by `tex`? Perfect time to apply a transition effect:

```
zoom (prox (0.5 + sin 0.03)) $ tex "https://upload.wikimedia.org/wikipedia/commons/4/43/Telephone_Box_with_a_View_%28geograph_2506046%29.jpg" fxy >> video <20>;
```

---

# Exercises:

1. Investigate blending `tex` using variables and arithmetic functions. Start with the following example
```
a << tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
b << texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
min a b >> video;
```

2. Add one (or more) of `move`, `tile` or `zoom` functions to the `tex` function being assigned to the `a` variable, what happens?
```
-- example
a << move ((sin 0.25)/4) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
b << texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
min a b >> video;
```

3. Try applying different arithmetic functions to the `video` output line, example:
```
a << move ((sin 0.25)/4) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
b << texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
a <= b >> video;
```

3. Try using different arithmetic functions with your variables, example:
```
a << move ((sin 0.25)/4) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
b << texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
a <= b >> hsv;
```

4. Investigate using outputs that aren't `video`, example:
```
a << move ((sin 0.25)/4) $ tex "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
b << texhsv "https://upload.wikimedia.org/wikipedia/commons/6/62/Section_of_the_brain%2C_19th_century._Wellcome_L0000838.jpg" fxy;
a <= b >> hsv;
```

---
