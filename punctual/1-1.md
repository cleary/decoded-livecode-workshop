# Punctual Visual Workshop 1-1 - First Creations (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Setup Cells!

```
!presetview fulltexteditor
```

Choose **Punctual** from the language dropdown

## Shapes and Syntax

```
circle [0,0] 0.25 >> video;
```
Position `[0,0]` is the centre of the screen. A positive coordinate moves it **left** (`x` axis) or **up** (`y` axis). A negative value moves it **right** (`x`axis) or **down** (`y` axis). 

Try moving it around
```
circle [0.5,-0.8] 0.25 >> video;
```
*Note that moving it more than `1` unit in any direction places the coordinate off-screen!*

Change the size (radius) by modifying the third number
```
circle [0.5,-0.8] 0.75 >> video;
```

`circle` is defined in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) as:
```
circle [x,y,...] [r] -- returns 1 when current fragment within a circle at x and y with radius r
```

Let's map this definition onto our example, so we can try and make sense of the all the definitions in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) 

Firstly, we can see that `circle` matches the **function** name
```
circle ... >> video;
```

Our first set of `[ ]` brackets contain the `[x,y,...]` coordinates - this is known as an **argument** or **parameter** to a function 

```
circle [0.5,-0.8] ... >> video;
```

And the last **argument** or **parameter** is the radius `[r]` of the circle

```
circle [0.5,-0.8] 0.25 >> video;
```

The `...` inside the square brackets means that you can specify more `x` and `y` coordinates for the `circle` function. Try it!
```
circle [0.5,-0.8,0.2,0.3] 0.25 >> video;
```
Now we have two circles - you can keep making as many as you want!

## More Shapes

Here are some more shape functions, and their definition in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md)

`vline` is a vertical line - lines in Punctual extend the width or height (in this case) of the screen, so for a `vline` we only need an `x` coordinate, and a line thickness, or `w`idth

*Note that prefixing a line with `--` creates a "comment" that Punctual will just ignore*

```
-- vline [x] [w]
vline 0 0.002 >> video;
```

Similarly for `hline` (horizontal line), except on the `y` axis
```
-- hline [y] [w]
hline 0.3 0.002 >> video;
```

`rect` is a rectangle that requires both `[x,y]` coordinates, as well as `w`idth and `h`eight arguments
```
-- rect [x,y,...] [w,h,...]
rect [0,0] [0.5,0.6] >> video;
```

`point` is just a pixel (and very small!). It has a fixed width and height, so the only arguments it needs are `[x,y]` coordinates
```
-- point [x,y,...]
point [0.5,0.5] >> video;
```
It's so small! Can you find it based on the `x,y` coordinates given?

## Channels on the `video` output

We can replace any individual number with a **list** of numbers contained in `[ ]`. When we do this we get different results on different colour "channels" of the `video` output - `red`, `green`, and `blue`:

Create three `vline` objects, one for each of the `red`, `green`, and `blue` channels by creating multiple `[x]` coordinate arguments
```
vline [-0.25,0,0.25] 0.002 >> video;
```

Notice that all the lines are the same `w`idth - why is that?

If you don't specify a position for at least three channels, remaining channels share the last value in the list

Create a `vline` specifying only two `x` coordinates
```
vline [-0.25,0.25] 0.002 >> video;
```

Notice the line on the right (`x` coordinate `0.25`) is a light blue colour, which is the combination of both `green` and `blue` channels (who are sharing the same `x` coordinate)

In this next example, we specify **four** `x` coordinates. The first 3 values are assigned to `red`, `green`, `blue` respectively, but the last value creates a fourth white `vline` - why is that?

```
vline [0.25, 0, -0.25, 0.5] 0.002 >> video;
```

Values can also be specified as fractions, using the Haskell Language syntax

```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```
---

# Exercises:

1. Create a scene with at least two different shapes

2. Try modifying the width of a `vline` - what value of the `w` parameter fills the screen?
```
vline 0 0.002 >> video;
```

3. How wide is a line width of `1`?

4. Using the `vline` example below which is specifying a different position for each of the `red`, `green`, and `blue` channels, how can you also specify multiple different line widths?
```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```

## For Bonus Points

5. Using `vline`, can you make an entirely `blue` screen?

6. Can you split the screen into thirds (using any of our shapes), with each third showing a different `RGB` colour?
 
*Hint: begin with the fraction example in **Ex 4.***

---

Next: [1-2. Moving Things Around](/punctual/1-2.md)
