# Punctual Visual Workshop 1-1 - First Creations (~1h)

Ref: 
 - https://estuary.mcmaster.ca
 - https://github.com/dktr0/Punctual/blob/main/REFERENCE.md

## Setup Cells!

`!presetview fulltexteditor`

Choose Punctual from the language dropdown

## Shapes and Syntax

```
circle [0,0] 0.25 >> video;
```

Move it left and right (x axis), up and down (y axis)
```
circle [0.5,-0.8] 0.25 >> video;
```

Change the size (radius)
```
circle [0.5,-0.8] 0.75 >> video;
```

`circle` is defined in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) as:
```
circle [x,y,...] [r] -- returns 1 when current fragment within a circle at x and y with radius r
```

Let's map this definition onto our example, so we can try and make sense of the all the definitions in the [Punctual Reference](https://github.com/dktr0/Punctual/blob/main/REFERENCE.md) 

Firstly, we can see that `circle` matches the **function** name
```
circle ... >> video;
```

Our first set of `[ ]` brackets contain the `[x,y,...]` coordinates - this is known as an **argument** or **parameter** to a function 

```
circle [0.5,-0.8] ... >> video;
```

And the last **argument** or **parameter** is the radius `[r]` of the circle

```
circle [0.5,-0.8] 0.25 >> video;
```

## More Shapes

`vline` is a vertical line - lines in Punctual extend the width or height (in this case) of the screen, so for a `vline` we only need an `x` coordinate, and a line thickness, or `w`idth

***Note that prefixing a line with `--` creates a "comment" that Punctual will just ignore***

```
-- vline [x] [w]
vline 0 0.002 >> video;
```

Similarly for `hline` (horizontal line), except on the `y` axis
```
-- hline [y] [w]
hline 0.3 0.002 >> video;
```

`rect` is a rectangle that requires both `[x,y]` coordinates, as well as `w`idth and `h`eight arguments
```
-- rect [x,y,...] [w,h,...]
rect [0,0] [0.5,0.6] >> video;
```

`point` is just a pixel (and very small!). It has a fixed width and height, so the only arguments it needs are `[x,y]` coordinates
```
-- point [x,y,...]
point [0.5,0.5] >> video;
```

## Channels on the `video` output

We can replace any individual number with a **list** of numbers contained in `[ ]`. When we do this we get different results on different colour "channels" of the `video` output - red, green, and blue:

Create three `vline` objects, one for each of the `red`, `green`, and `blue` channels by creating multiple `[x]` coordinate arguments
```
vline [-0.25,0,0.25] 0.002 >> video;
```

Notice that all the lines are the same `w`idth - why is that?

If you don't specify a position for at least three channels, remaining channels share the last value in the list

Create a `vline` specifying only two `x` coordinates
```
vline [-0.25,0.25] 0.002 >> video;
```

Notice the line on the right (x-coordinate `0.25`) is a light blue colour, which is the combination of both Green and Blue channels (who are sharing the same `x` coordinate)

In this next example, we specify **four** x coordinates. The first 3 values are assigned to `red`, `green`, `blue` respectively, but the last value creates a fourth white `vline` - why is that?

```
vline [0.25, 0, -0.25, 0.5] 0.002 >> video;
```

Values can also be specified as fractions, using the Haskell Language syntax

```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```
---

# Exercises:

1. Try modifying the width of a `vline` - what value of the `w` parameter fills the screen?



2. Using this multi `vline` example below, how can you specify multiple different line widths?
```
vline [(-2)/3, 0, 2/3] 0.002 >> video;
```


3. How wide is a line width of `1`?


4. Create a scene with at least two different shapes


## For Bonus Points

5. Using `vline`, can you make an entirely `blue` screen?



6. Can you split the screen into thirds (using any of our shapes), with each third showing a different RGB colour? (Hint: see the fraction example above)



---
